/*
 * Adapted from The MIT License (MIT)
 *
 * Copyright (c) 2020-$today.year DaPorkchop_
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,
 * modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software
 * is furnished to do so, subject to the following conditions:
 *
 * Any persons and/or organizations using this software must include the above copyright notice and this permission notice,
 * provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
 * BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

#ifndef COMP_FRUSTUM
#define COMP_FRUSTUM

#include <"fp2:shaders/comp/common.comp">

//
//
// MACROS
//
//

#define CLIPPING_PLANES_FULL_OR (4)
#define CLIPPING_PLANES_FULL_AND (2)
#define MAX_CLIPPING_PLANES_PARTIAL (10)

//
//
// UNIFORMS
//
//

//Frustum

layout(std140, binding = 2) uniform FRUSTUM {
    vec4 fullPlanesOR[CLIPPING_PLANES_FULL_OR];
    vec4 fullPlanesAND[CLIPPING_PLANES_FULL_AND];

    uint partialCount;
    vec4 partialPlanes[MAX_CLIPPING_PLANES_PARTIAL];
} frustum;

//
//
// UTILITIES
//
//

bool testOutsideClippingPlaneOR(in vec3 mn, in vec3 mx, in vec4 plane) {
    return 0.0 >= min(
                    min(
                        min(
                            dot(plane, vec4(mn.x, mn.y, mn.z, 1.0)),
                            dot(plane, vec4(mn.x, mn.y, mx.z, 1.0))),
                        min(
                            dot(plane, vec4(mn.x, mx.y, mn.z, 1.0)),
                            dot(plane, vec4(mn.x, mx.y, mx.z, 1.0)))),
                    min(
                        min(
                            dot(plane, vec4(mx.x, mn.y, mn.z, 1.0)),
                            dot(plane, vec4(mx.x, mn.y, mx.z, 1.0))),
                        min(
                            dot(plane, vec4(mx.x, mx.y, mn.z, 1.0)),
                            dot(plane, vec4(mx.x, mx.y, mx.z, 1.0)))));
}

bool testOutsideClippingPlaneAND(in vec3 mn, in vec3 mx, in vec4 plane) {
    return 0.0 >= max(
                    max(
                        max(
                            dot(plane, vec4(mn.x, mn.y, mn.z, 1.0)),
                            dot(plane, vec4(mn.x, mn.y, mx.z, 1.0))),
                        max(
                            dot(plane, vec4(mn.x, mx.y, mn.z, 1.0)),
                            dot(plane, vec4(mn.x, mx.y, mx.z, 1.0)))),
                    max(
                        max(
                            dot(plane, vec4(mx.x, mn.y, mn.z, 1.0)),
                            dot(plane, vec4(mx.x, mn.y, mx.z, 1.0))),
                        max(
                            dot(plane, vec4(mx.x, mx.y, mn.z, 1.0)),
                            dot(plane, vec4(mx.x, mx.y, mx.z, 1.0)))));
}

/**
 * Checks to see whether or not the given AABB is totally contained by the frustum.
 *
 * @return false if any part of the AABB is outside of the frustum, true otherwise
 */
bool isBoxInFrustumFully(in vec3 min, in vec3 max) {
    for (uint i = 0; i < CLIPPING_PLANES_FULL_OR; i++) {
        if (testOutsideClippingPlaneOR(min, max, frustum.fullPlanesOR[i])) {
            return false;
        }
    }
    for (uint i = 0; i < CLIPPING_PLANES_FULL_AND; i++) {
        if (testOutsideClippingPlaneAND(min, max, frustum.fullPlanesAND[i])) {
            return false;
        }
    }
    return true;
}

/**
 * Checks to see whether or not the given AABB is partially contained by the frustum.
 *
 * @return false if the entire AABB is outside of the frustum, true otherwise
 * @see #isBoxInFrustum(vec3, vec3)
 */
bool isBoxInFrustumPartial(in vec3 min, in vec3 max) {
    for (uint planeIdx = 0, planeCount = frustum.partialCount; planeIdx < planeCount; planeIdx++) {
        if (testOutsideClippingPlaneAND(min, max, frustum.partialPlanes[planeIdx])) {
            return false;
        }
    }
    return true;
}

/**
 * Checks to see whether or not the given AABB is partially contained by the frustum.
 *
 * @return false if the entire AABB is outside of the frustum, true otherwise
 */
bool isBoxInFrustum(in vec3 min, in vec3 max) {
    return isBoxInFrustumFully(min, max) || isBoxInFrustumPartial(min, max);
}

#endif //COMP_FRUSTUM
